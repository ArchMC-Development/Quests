plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.0'
    id 'org.jetbrains.kotlin.jvm' version '2.1.10'
    id 'org.ajoberstar.grgit' version '5.3.0'
}

var currentBranch = grgit.branch.current().name
if (currentBranch != "main") {
    println("Starting in development mode")
}

group = 'lol.arch.quests'
version = '1.0.0'
sourceCompatibility = '21'
targetCompatibility = '21'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
    maven {
        url = "${property("artifactory_contextUrl")}/gradle-release"
        credentials {
            username = property("artifactory_user")
            password = property("artifactory_password")
        }
    }
}

dependencies {
    compileOnly("org.jetbrains.kotlin:kotlin-stdlib:2.1.10")
    compileOnly 'org.jetbrains:annotations:24.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'dev.dejvokep:boosted-yaml:1.3'
    // arch start
    compileOnly 'gg.scala.store:spigot:1.0.0'
    compileOnly "gg.scala.commons:bukkit:4.1.7"
    compileOnly("gg.scala.cloudsync:spigot:1.0.4")
    // arch end

    implementation 'fr.mrmicky:FastInv:3.0.4'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.20:3.65.0'
    testImplementation 'org.mockito:mockito-core:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    testImplementation 'org.junit.platform:junit-platform-runner:1.2.0'
}

test {
    useJUnitPlatform()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '21'
        freeCompilerArgs = ['-Xjvm-default=all']
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar {
    relocate 'fr.mrmicky.fastinv', 'lol.arch.quests.libs'

    archiveClassifier.set('')
    destinationDirectory.set(file("$rootDir/target"))
}

publishing {
    repositories {
        maven {
            url = property("artifactory_contextUrl") + (currentBranch == "main" ? "/gradle-release" : "/gradle-dev")
            name = "arch"
            credentials {
                username = property("artifactory_user").toString()
                password = property("artifactory_password").toString()
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = 'lol.arch.quests'
            artifactId = 'Quests'
            version = "$rootProject.version"
            artifact shadowJar
        }
    }
}

tasks.getByName("build").dependsOn(publishMavenJavaPublicationToArchRepository)
jar.dependsOn shadowJar